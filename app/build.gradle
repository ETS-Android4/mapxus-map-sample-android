apply plugin: 'com.android.application'


def secretPropertiesFile = rootProject.file("./secret.properties")
def secretProperties = new Properties()
secretProperties.load(new FileInputStream(secretPropertiesFile))

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.mapxus.mapxusmapandroiddemo"

        minSdkVersion 21
        targetSdkVersion 29

        versionCode 4
        versionName 'v1.0.0'

        vectorDrawables.useSupportLibrary = true

        buildConfigField "int", "MIN_SDK_VERSION", "$minSdkVersion.apiLevel"
        ndk {
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "MapxusMapSampleApp_${defaultConfig.versionName}_${buildType.name}.apk"
        }
    }

    buildTypes {
        debug {
            manifestPlaceholders = ["appid": secretProperties['appid'], "secret": secretProperties['secret']]
        }
    }

    lintOptions {
        abortOnError false
    }

}

dependencies {
    // Support libraries
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "pub.devrel:easypermissions:1.2.0"

    // MapxusMap
    implementation "com.mapxus.visual:mapxusvisual:0.2.0"
    implementation "com.mapxus.map:mapxusmap:4.0.0"
    implementation 'com.mapxus.positioning:positioning:2.0.0'

    // Firebase
    implementation "com.google.firebase:firebase-crash:11.0.4"
    implementation "com.google.firebase:firebase-perf:11.0.4"

    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'

    // Square crew
    implementation "com.squareup.picasso:picasso:2.5.2"

    //mapbox annotation
    implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-annotation-v9:0.8.0'
}

apply from: "${rootDir}/gradle/checkstyle.gradle"
